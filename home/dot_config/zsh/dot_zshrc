[[ -r "${XDG_CONFIG_HOME:-$HOME/.config}/shell/aliasi.sh" ]] && source "${XDG_CONFIG_HOME:-$HOME/.config}/shell/aliasi.sh"

if [[ -n $GHOSTTY_RESOURCES_DIR ]]; then
  source "$GHOSTTY_RESOURCES_DIR"/shell-integration/zsh/ghostty-integration
fi

if [[ -n "$SSH_CONNECTION" ]]; then
    unset VISUAL
    export BROWSER="local-open"
fi

if [[ -n "$KITTY_INSTALLATION_DIR" ]]; then
    export KITTY_SHELL_INTEGRATION="no-cursor no-title"
    autoload -Uz -- "$KITTY_INSTALLATION_DIR"/shell-integration/zsh/kitty-integration
    kitty-integration
    unfunction kitty-integration
    alias ssh="kitten ssh"
fi

if [[ -n "$KITTY_INSTALLATION_DIR" || -n "$_KITTY_IS_REMOTE" ]]; then
    autoload -Uz add-zsh-hook
    _kitty_uservar() { printf '\e]1337;SetUserVar=%s=%s\a' "$1" "$(printf '%s' "$2" | base64)" }
    _kitty_preexec() {
        _kitty_uservar PROC "${3%% *}"
        print -Pn "\e]0;${3%% *}\a"
    }
    _kitty_precmd() {
        _kitty_uservar PROC "${SHELL:t}"
        if [[ -n "$_KITTY_IS_REMOTE" ]]; then
            _kitty_uservar REMOTE_HOST "$HOST"
        else
            _kitty_uservar REMOTE_HOST ""
        fi
        print -Pn "\e]0;%~\a"
    }
    add-zsh-hook preexec _kitty_preexec
    add-zsh-hook precmd _kitty_precmd
elif [[ -n "$SSH_CONNECTION" ]]; then
    autoload -Uz add-zsh-hook
    _ssh_precmd() { print -Pn "\e]0;%~\a" }
    add-zsh-hook precmd _ssh_precmd
fi

if [[ -n "$KMUX_MODE" && -z "$ZMX_SESSION" ]] && command -v zmx &>/dev/null; then
    if [[ -n "$KMUX_SESSION" ]]; then
        exec zmx attach "$KMUX_SESSION"
    elif [[ -n "$KITTY_WINDOW_ID" ]]; then
        exec zmx attach "kmux-${KITTY_WINDOW_ID}"
    fi
fi

HISTFILE="${XDG_STATE_HOME}/zsh/history"
HISTSIZE=290000
SAVEHIST=$HISTSIZE
HISTDUP=erase

[[ ! -d "${XDG_STATE_HOME}/zsh" ]] && mkdir -p "${XDG_STATE_HOME}/zsh"
if [[ -f "${ZDOTDIR:-$HOME}/.zsh_history" ]]; then
    mv "${ZDOTDIR:-$HOME}/.zsh_history" "${XDG_STATE_HOME}/zsh/history"
fi

if [[ -o interactive ]] && command -v starship &>/dev/null; then
    eval "$(starship init zsh)"

    STARSHIP_TRANSIENT_PROMPT="${${PROMPT[@]:2:-1}// prompt / prompt --profile transient }"
    STARSHIP_TRANSIENT_RPROMPT="${${PROMPT[@]:2:-1}// prompt / prompt --profile rtransient }"

    function starship-transient-prompt-precmd {
        STARSHIP_SAVED_PROMPT="$(eval "${STARSHIP_TRANSIENT_PROMPT}")"
        STARSHIP_SAVED_RPROMPT="$(eval "${STARSHIP_TRANSIENT_RPROMPT}")"

        # Fix ctrl+c behavior
        TRAPINT() { starship-transient-prompt 2>/dev/null; return $(( 128 + $1 )) }
    }

    autoload -Uz add-zsh-hook
    add-zsh-hook precmd starship-transient-prompt-precmd

    function starship-transient-prompt() {
        if [[ "$PWD" == "$STARSHIP_SAVED_PROMPT_PWD" ]]; then
            PROMPT="$STARSHIP_SAVED_PROMPT" RPROMPT="$STARSHIP_SAVED_RPROMPT" zle .reset-prompt
        else
            STARSHIP_SAVED_PROMPT_PWD="$PWD"
        fi
    }

    autoload -Uz add-zle-hook-widget
    add-zle-hook-widget zle-line-finish starship-transient-prompt

    function clear-screen() {
        # Save transient prompt before clearing screen
        starship-transient-prompt-precmd
        zle .clear-screen
    }

    zle -N clear-screen
fi

typeset -A ZINIT=(
  BIN_DIR         ${XDG_DATA_HOME:-$HOME/.local/share}/zinit/bin
  HOME_DIR        ${XDG_DATA_HOME:-$HOME/.local/share}/zinit
  ZCOMPDUMP_PATH  ${XDG_CACHE_HOME:-$HOME/.cache}/zcompdump-${(%):-%n}
  COMPINIT_OPTS   -C
)

ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
if [[ ! -d "${ZINIT_HOME}" ]]; then
  mkdir -p "$(dirname "$ZINIT_HOME")"
  git clone https://github.com/zdharma-continuum/zinit.git "${ZINIT_HOME}"
fi

source "${ZINIT_HOME}/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

bindkey -e

for file in utils.zsh local.zsh; do
  if [[ -r "$ZDOTDIR/$file" ]]; then
      source "$ZDOTDIR/$file"
  fi
done

zinit light-mode for \
  zdharma-continuum/zinit-annex-as-monitor \
  zdharma-continuum/zinit-annex-bin-gem-node \
  zdharma-continuum/zinit-annex-patch-dl \
  zdharma-continuum/zinit-annex-rust

zinit ice as"command" from"gh-r" bpick"atuin-*.tar.gz" mv"atuin*/atuin -> atuin" \
  atclone"./atuin init zsh > init.zsh; ./atuin gen-completions --shell zsh > _atuin" \
  atpull"%atclone" src"init.zsh" \
  atload'
    bindkey "^E" _atuin_search_widget
    export ATUIN_NOBIND="true"
  '
zinit light atuinsh/atuin

zinit ice as"program" mv"httpstat.sh -> httpstat" \
  pick"httpstat" atpull'!git reset --hard'
zinit light b4b4r07/httpstat

omzLib git.zsh
omzLib functions.zsh
omzLib clipboard.zsh

if [[ "$(uname)" = "Darwin" ]]; then
  zinit light cwelsys/zsh-darwin
  omzPlugin brew
else
  omzPlugin systemd
fi

omzPlugin git
omzPlugin sudo
omzPlugin web-search
omzPlugin fancy-ctrl-z
omzPlugin copypath
omzPlugin copyfile
omzPlugin dirhistory
omzPlugin colored-man-pages
omzPlugin jsontools
omzPlugin chezmoi
omzPlugin gpg-agent
omzPlugin github
omzPlugin direnv

zinit wait lucid light-mode for \
  ryanccn/vivid-zsh \
  Aloxaf/fzf-tab \
  djui/alias-tips \
  atload'_zsh_autosuggest_start' \
    zsh-users/zsh-autosuggestions \
  blockf atpull'zinit creinstall -q .' \
    zsh-users/zsh-completions \
  hlissner/zsh-autopair \
  reegnz/jq-zsh-plugin \
  denisidoro/navi \
  laggardkernel/zsh-thefuck \
  paulirish/git-open \
  Freed-Wu/fzf-tab-source \
  atinit'zicompinit;
  zicdreplay' \
    zdharma-continuum/fast-syntax-highlighting \

[[ -r "${XDG_CONFIG_HOME:-$HOME/.config}/shell/ctpgum.sh" ]] && source "${XDG_CONFIG_HOME:-$HOME/.config}/shell/ctpgum.sh" mocha blue

setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_reduce_blanks
setopt hist_ignore_space
setopt inc_append_history
setopt share_history
setopt autocd
setopt multios
setopt globdots
setopt extended_glob
setopt nobgnice
setopt bang_hist
setopt nobeep

zstyle ":completion:*" matcher-list "" "m:{a-z}={A-Z}" "m:{a-zA-Z}={A-Za-z}" "r:|[._-]=* r:|=* l:|=*"
zstyle ':fzf-tab:*' fzf-min-height 15
zstyle ':fzf-tab:*' popup-pad 30 0
zstyle ':fzf-tab:*' prefix ''
zstyle ':fzf-tab:*' switch-group ',' '.'
zstyle ':fzf-tab:*' continuous-trigger 'tab'
zstyle ':fzf-tab:*' use-fzf-default-opts yes
zstyle ':fzf-tab:*' fzf-flags --color=fg:1,fg+:2 --bind=tab:accept
zstyle ':completion:*' menu no
zstyle ':completion:*:git-checkout:*' sort false
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':fzf-tab:complete:*:*' fzf-preview 'less ${(Q)realpath}'
zstyle ':fzf-tab:complete:*:*' fzf-flags --preview-window 'right,60%,wrap,<36(down,wrap,70%)'
zstyle ':fzf-tab:complete:*:options' fzf-preview
zstyle ':fzf-tab:complete:*:argument-1' fzf-preview
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompcache"
zstyle ':fzf-tab:complete:yay:*' fzf-preview 'yay -Si $word'
zstyle ':fzf-tab:complete:-command-:*' fzf-preview \
    '(out=$(tldr --color "$word") 2>/dev/null && echo $out) || (out=$(MANWIDTH=$FZF_PREVIEW_COLUMNS man "$word") 2>/dev/null && echo $out) || (out=$(which "$word") && echo $out) || echo "${(P)word}"'
zstyle ':fzf-tab:complete:tldr:argument-1' fzf-preview 'tldr --color $word'
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:*' fzf-bindings 'ctrl-a:toggle-all'
zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm -w -w"
zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-preview \
  '[[ $group == "[process ID]" ]] && ps --pid=$word -o cmd --no-headers -w -w'
zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-flags --preview-window=down:3:wrap
zstyle ':fzf-tab:complete:systemctl-*:*' fzf-preview 'SYSTEMD_COLORS=1 systemctl status $word'
zstyle ':fzf-tab:complete:systemctl:--user:*' fzf-preview 'SYSTEMD_COLORS=1 systemctl --user status $word'
zstyle ':fzf-tab:complete:(-command-|-parameter-|-brace-parameter-|export|unset|expand):*' \
	fzf-preview 'echo ${(P)word}'
zstyle ':fzf-tab:complete:git-(add|diff|restore):*' fzf-preview \
	'git diff $word | delta'
zstyle ':fzf-tab:complete:git-log:*' fzf-preview \
	'git log --color=always $word'
zstyle ':fzf-tab:complete:git-show:*' fzf-preview \
	'case "$group" in
	"commit tag") git show --color=always $word ;;
	*) git show --color=always $word | delta ;;
	esac'
zstyle ':fzf-tab:complete:git-checkout:*' fzf-preview \
	'case "$group" in
	"modified file") git diff $word | d ;;
	"recent commit object name") git show --color=always $word | delta ;;
	*) git log --color=always $word ;;
	esac'

bindkey ' ' magic-space
bindkey '^[[3~' delete-char
bindkey '^[[1;5D' beginning-of-line
bindkey '^[[1;5C' end-of-line
bindkey "^N" insert-last-word
bindkey '^[w' kill-region
bindkey '^Y' redo

[ -x "$(command -v zoxide)" ] && eval "$(zoxide init --cmd cd zsh)"
